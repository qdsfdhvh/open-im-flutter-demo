def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    namespace = "cn.rentsoft.flutter.openim.consumer"
    compileSdkVersion 34
    ndkVersion flutter.ndkVersion

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "cn.rentsoft.flutter.openim.consumer"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion 24
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        multiDexEnabled true

        ndk {
            abiFilters "arm64-v8a"//, "armeabi-v7a", "armeabi", "x86", "x86_64", "mips", "mips64"
        }

        lintOptions {
            disable 'InvalidPackage'
            disable "Instantiatable"
            checkReleaseBuilds false
            abortOnError false
        }

        manifestPlaceholders = [
                GETUI_APPID    : "",
                // 下面是多厂商配置，如需要开通使用请联系技术支持
                // 如果不需要使用，预留空字段即可
                XIAOMI_APP_ID  : "",
                XIAOMI_APP_KEY : "",
                MEIZU_APP_ID   : "",
                MEIZU_APP_KEY  : "",
                HUAWEI_APP_ID  : "",
                OPPO_APP_KEY   : "",
                OPPO_APP_SECRET: "",
                VIVO_APP_ID    : "",
                VIVO_APP_KEY   : "",
                // 荣耀相关应用参数
                HONOR_APP_ID   : "",
        ]
    }

    signingConfigs {
        release {
            keyAlias 'openim'
            keyPassword 'openim'
            storeFile file('./openim')
            storePassword 'openim'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        profile {
            signingConfig signingConfigs.release

        }
        release {
//            minifyEnabled false
//            useProguard false
//            shrinkResources false
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "OpenIM_${versionName}(${versionCode})_${buildType.name}_${buildTime()}.apk"
            }
    }
}

flutter {
    source '../..'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'com.huawei.hms:push:6.12.0.300'
    implementation 'com.getui.opt:hwp:3.1.0'   // huawei
    implementation 'com.getui.opt:xmp:3.2.0'   // mi
    implementation('com.assist-v3:oppo:3.1.0') {
        exclude group: 'commons-codec'
    }   // oppo
    implementation 'com.assist-v3:vivo:3.1.0'  // vivo
    implementation 'com.getui.opt:mzp:3.2.0'   // meizu
    implementation 'com.getui.opt:ups:3.0.3'   // ups，ups
    implementation 'com.getui.opt:honor:3.2.0' // honor

    implementation 'com.android.support:multidex:2.0.1' //enter the latest multidex version
}

//apply plugin: 'com.huawei.agconnect'
//apply plugin: 'com.google.gms.google-services'

static def buildTime() {
    return new Date().format("MMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}